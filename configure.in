dnl $Id$

dnl Process this file with autoconf to produce a configure script.

dnl This is the only place where the package version appears
dnl (apart from changelogs & NEWS)
AC_INIT(surfraw, 2.3.0)
AC_PREREQ(2.54)
AC_CONFIG_SRCDIR([surfraw.IN])

dnl minimum automake version, not surfraw version
AM_INIT_AUTOMAKE([1.10])

AC_MSG_CHECKING([whether to enable local OpenSearch support])
AC_ARG_ENABLE([opensearch],
	[AS_HELP_STRING([--disable-opensearch], [disable local OpenSearch support])],,
	[enable_opensearch=yes])
AC_MSG_RESULT([$enable_opensearch])
AM_CONDITIONAL(OPENSEARCH, test "$enable_opensearch" = "yes")

AC_MSG_CHECKING([whether to install sr alias])
AC_ARG_ENABLE([sr],
	[AS_HELP_STRING([--disable-sr], [disable sr alias])],,
	[enable_sr=yes])
AC_MSG_RESULT([$enable_sr])
AM_CONDITIONAL(INSTALLSR, test "$enable_sr" = "yes")

ELVIDIR="\$(libdir)/surfraw"
AC_CHECK_PROGS(TEXTBROWSER, lynx links2 links elinks w3m netrik)
AC_CHECK_PROGS(GRAPHICALBROWSER, iceweasel firefox google-chrome opera mozilla \
    xombrero luakit safari epiphany-browser konqueror galeon kazenhakase \
    conkeror dillo netscape chromium chrome)

AC_ARG_WITH([elvidir],
	[AS_HELP_STRING([--with-elvidir=ELVIDIR], [specify location of elvi])],
	[if test -n "$withval" && test "$withval" != "no" ; then ELVIDIR="$withval";fi])

AC_ARG_WITH([text-browser],
	[AS_HELP_STRING([--with-text-browser=BROWSER], [specify default text browser])],
	[if test -n "$withval" && test "$withval" != "no" ; then TEXTBROWSER="$withval";fi])
AC_ARG_WITH([graphical-browser],
	[AS_HELP_STRING([--with-graphical-browser=BROWSER], [specify default graphical browser])],
	[if test -n "$withval" && test "$withval" != "no" ; then GRAPHICALBROWSER="$withval";fi])

# Bash completions!
# I'm not sure what to do if bash-completion isn't installed.
AC_ARG_WITH([bash-completion-dir],
	[AS_HELP_STRING([--with-bash-completion-dir[=PATH]],
		[Install the bash auto-completion script in this directory. NOTE: Not guaranteed to install under $prefix.])],
	[],
	[with_bash_completion_dir=yes])
PKG_CHECK_VAR([BASH_COMPLETION_DIR], [bash-completion >= 2.0], [completionsdir])
AC_MSG_CHECKING([for bash completions directory])
AS_IF([test "x$BASH_COMPLETION_DIR" = "x"],
	[AC_MSG_WARN([could not find bash completions dir. using default value])
	 BASH_COMPLETION_DIR="$datadir/bash-completion/completions"])
AC_MSG_RESULT([$BASH_COMPLETION_DIR])
# Handle user passing a directory as the argument to the option.
AS_IF([test "x$BASH_COMPLETION_DIR" != "xno"],
	[AC_MSG_NOTICE([using user-provided bash completions directory... $with_bash_completion_dir])
	 BASH_COMPLETION_DIR="$with_bash_completion_dir"])
AM_CONDITIONAL([WITH_BASH_COMPLETION], [test "x$with_bash_completion_dir" != "xno"])

AC_SUBST(ELVIDIR)
AC_SUBST(VERSION)
AC_SUBST(TEXTBROWSER)
AC_SUBST(GRAPHICALBROWSER)
AC_SUBST([BASH_COMPLETION_DIR])

ISODATE=`date -u -r ChangeLog +%Y-%m-%d`
AC_SUBST(ISODATE)

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_AWK
AC_PATH_PROG([PERL], [perl], [perl])
AC_SUBST(PERL)

AC_CONFIG_FILES([Makefile surfraw.lsm surfraw.spec elvi/Makefile test/Makefile examples/Makefile])
AC_OUTPUT
